
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!"\n" ~ ANY)*)  }

integer = @{ ASCII_DIGIT+ }
float = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }

sqstring = @{ "'" ~ ( "''" | (!"'" ~ ANY) )* ~ "'" }
dqstring = @{ "\"" ~ ( "\"\"" | (!"\"" ~ ANY) )* ~ "\"" }
string = {sqstring | dqstring}
number = _{integer | float}

name = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

parameter = _{ name }
parameterlist = {parameter ~ ("," ~ parameter)* ~ ","?}
function = {"function" ~ name? ~ "(" ~ parameterlist? ~ ")" ~ block}


argument = _{ expr }
argumentlist = {argument ~ ("," ~ argument)* ~ ","?}
functioncall = {"(" ~ argumentlist? ~ ")"}
trailer = _{functioncall}

atom = _{ function | name | number | string | "(" ~ expr ~ ")" }

atomexpression = {atom ~ trailer?}
factor = {negate ~ factor | atomexpression }
negate = {"-"}

term = {factor ~ ((multiply|divide) ~ factor)*}
multiply = {"*"}
divide = {"/"}

expr = { term ~ ((add|subtract) ~ term)* }
add = {"+"}
subtract = {"-"}

assignment = {name ~ "=" ~ expr ~ semicolon}
semicolon = { ";" }

expression = _{expr ~ semicolon}

block = { "{" ~ statement* ~ "}" }

returnstatement = { "return" ~ expr ~ semicolon}

blockstatement = _{function}
statement = { assignment | blockstatement | returnstatement | expression}


file = { SOI ~ (statement)* ~ EOI }


